cmake_minimum_required(VERSION 3.6)

project(
    kedixa
    VERSION 0.1.1
    LANGUAGES CXX
)

set(KEDIXA_LIBRARY_NAME ${PROJECT_NAME})
set(KEDIXA_LIBRARY_DIR ${PROJECT_BINARY_DIR}/lib)
set(KEDIXA_INCLUDE_DIR ${PROJECT_BINARY_DIR}/include)
set(KEDIXA_INSTALL_CMAKE_FILE ${PROJECT_BINARY_DIR}/${KEDIXA_LIBRARY_NAME}-config.install.cmake)

set(CMAKE_BUILD_TYPE RelWithDebInfo)
set(CMAKE_SKIP_RPATH TRUE)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${KEDIXA_LIBRARY_DIR})
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${KEDIXA_LIBRARY_DIR})

file(COPY
    ${PROJECT_SOURCE_DIR}/include/kedixa
    DESTINATION
    ${KEDIXA_INCLUDE_DIR}
)

add_subdirectory(src)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)
set(LIBRARY_NAME ${KEDIXA_LIBRARY_NAME})
set(INCLUDE_DIR ${KEDIXA_INCLUDE_DIR})
set(LIBRARY_DIR ${KEDIXA_LIBRARY_DIR})
configure_package_config_file(
    ${KEDIXA_LIBRARY_NAME}-config.cmake.in
    ${PROJECT_BINARY_DIR}/${KEDIXA_LIBRARY_NAME}-config.cmake
    INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${KEDIXA_LIBRARY_NAME}
    PATH_VARS INCLUDE_DIR LIBRARY_DIR LIBRARY_NAME
)

set(INCLUDE_DIR ${CMAKE_INSTALL_INCLUDEDIR})
set(LIBRARY_DIR ${CMAKE_INSTALL_LIBDIR})
configure_package_config_file(
    ${KEDIXA_LIBRARY_NAME}-config.cmake.in
    ${KEDIXA_INSTALL_CMAKE_FILE}
    INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${KEDIXA_LIBRARY_NAME}
    PATH_VARS INCLUDE_DIR LIBRARY_DIR LIBRARY_NAME
)

install(TARGETS
    ${KEDIXA_LIBRARY_NAME}
    LIBRARY DESTINATION lib
)

install(DIRECTORY
    ${KEDIXA_INCLUDE_DIR}/kedixa
    DESTINATION include)

install(FILES ${KEDIXA_INSTALL_CMAKE_FILE}
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${KEDIXA_LIBRARY_NAME}
    RENAME ${KEDIXA_LIBRARY_NAME}-config.cmake
)

set(CPACK_GENERATOR "RPM")
set(CPACK_PACKAGE_NAME "kedixa")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "kedixa: kedixa's personal c++ toy library")
set(CPACK_PACKAGE_VENDOR "kedixa")
set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})
# set(CPACK_RPM_PACKAGE_GROUP "")
set(CPACK_RPM_PACKAGE_URL "https://github.com/kedixa/klibcpp/")
# set(CPACK_RPM_PACKAGE_DESCRIPTION "")
set(CPACK_RPM_PACKAGE_LICENSE "MIT Licence")
set(CPACK_RPM_FILE_NAME RPM-DEFAULT)
set(CPACK_RPM_PACKAGE_RELEASE_DIST ON)
set(CPACK_RPM_PACKAGE_RELEASE "0")

include(CPack)
